#
# debian-squeeze
#

DESCRIPTION = "STLport C++ class library"
LICENSE = "AFL-3.0"
LIC_FILES_CHKSUM = "file://Makefile;beginline=6;endline=6;md5=2558fc61600ba4d9b3c9d15e24d69a76"

PR = "r0"

inherit debian-squeeze autotools
DEBIAN_SQUEEZE_SRCPKG_NAME = "stlport5.2"

# override configure options
# 1. --build, --host and some path options are not supported
# 2. final CC and CXX generated by --target and and --with-c*
#    (see configure and build/Makefiles/gmake/config.mak)
CONFIGUREOPTS = " \
--prefix=${prefix} \
--bindir=${bindir} \
--libdir=${libdir} \
--includedir=${includedir} \
--use-compiler-family=gcc \
--target=${TARGET_SYS} \
--with-cc=gcc \
--with-cxx=c++ \
"

# "--hash-style=gnu" (=) breaks LDFLAGS generated by Makefile
TARGET_LINK_HASH_STYLE = ""

# come from debian/rules
EXTRA_OEMAKE = "-C build/lib -f gcc.mak"

# FIXME: not supported?
PARALLEL_MAKE = ""

# need to specify targets
do_compile() {
	oe_runmake release-shared
}
do_install() {
	oe_runmake 'DESTDIR=${D}' install-release-shared
}
